#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ЗаписатьДанныеДокументаВРегистр();
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныйПрограммныйИнтерфейс


Процедура Рассчитать() Экспорт
	
	НачатьТранзакцию();
	
	ЗаписатьДанныеДокументаВРегистр();
	
	ПериодРегистрации = НачалоМесяца(Дата);
	НачислениеЗарплатыСервер.РассчитатьНачисления(Ссылка, Движения, ОсновныеНачисления, ДополнительныеНачисления, ПериодРегистрации);
	
	ОтменитьТранзакцию();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаписатьДанныеДокументаВРегистр()
	
	Движения.ОсновныеНачисления.Записывать = Истина;
	Движения.ОсновныеНачисления.Очистить();
	Движения.ОсновныеНачисления.Записать();
	
	Движения.ДополнительныеНачисления.Записывать = Истина;
	Движения.ДополнительныеНачисления.Очистить();
	Движения.ДополнительныеНачисления.Записать();
	
	ПериодРегистрации = НачалоМесяца(Дата);
	
	Для Каждого ОсновноеНачисление Из ОсновныеНачисления Цикл
		
		Движение = Движения.ОсновныеНачисления.Добавить();
		
		Движение.ПериодРегистрации = ПериодРегистрации;
		Движение.ВидРасчета = ОсновноеНачисление.ВидРасчета;
		
		Движение.ПериодДействияНачало = ОсновноеНачисление.ДатаНачала;
		Движение.ПериодДействияКонец = ОсновноеНачисление.ДатаОкончания;
		
		Движение.График = ОсновноеНачисление.График;
		Движение.Сотрудник = ОсновноеНачисление.Сотрудник;
		Движение.Результат = ОсновноеНачисление.Результат;
		Движение.Параметр = ОсновноеНачисление.Параметр;
		
	КонецЦикла;
	
	КонецБазовогоПериодаПремии = КонецМесяца(Дата);
	
	Для Каждого ДополнительноеНачисление Из ДополнительныеНачисления Цикл
		
		Движение = Движения.ДополнительныеНачисления.Добавить();
		
		Движение.ПериодРегистрации = ПериодРегистрации;
		Движение.ВидРасчета = ДополнительноеНачисление.ВидРасчета;
		
		Движение.БазовыйПериодНачало = ПериодРегистрации;
		Движение.БазовыйПериодКонец = КонецБазовогоПериодаПремии;
		
		Движение.Сотрудник = ДополнительноеНачисление.Сотрудник;
		Движение.Результат = ДополнительноеНачисление.Результат;
		Движение.Параметр = ДополнительноеНачисление.Параметр;
		Движение.Стаж = ДополнительноеНачисление.Стаж;
		
	КонецЦикла;

	Движения.ОсновныеНачисления.Записывать = Истина;
	Движения.ДополнительныеНачисления.Записывать = Истина;
	
	Движения.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
